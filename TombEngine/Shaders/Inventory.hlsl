#include "./CBCamera.hlsli"
#include "./CBItem.hlsli"
#include "./Blending.hlsli"
#include "./VertexInput.hlsli"
#include "./ShaderLight.hlsli"
#include "./ModernLighting.hlsli"
#include "./ObjectTransforms.hlsli"
#include "./AnimatedTextures.hlsli"
#include "./VertexEffects.hlsli"
#include "./Materials.hlsli"

struct PixelShaderInput
{
	float4 Position: SV_POSITION;
	float3 Normal: NORMAL0;
	float3 WorldPosition : POSITION;
	float2 UV: TEXCOORD;
	float4 Color: COLOR;
    float Sheen : SHEEN;
    float3 Tangent : TANGENT;
    float3 Binormal : BINORMAL;
    float3 FaceNormal : NORMAL1;
};

struct PixelShaderOutput
{
    float4 Color : SV_Target0;
    float4 Emissive : SV_Target1;
};
    
Texture2D Texture : register(t0);
SamplerState Sampler : register(s0);

Texture2D NormalTexture : register(t1);
SamplerState NormalTextureSampler : register(s1);

PixelShaderInput VS(VertexShaderInput input)
{
	PixelShaderInput output;

    float4x4 blended = Skinned ? BlendBoneMatrices(input, Bones, (Skinned == 2)) : Bones[input.BoneIndex[0]];
    float4x4 world = mul(blended, World);

	output.Position = mul(mul(float4(input.Position, 1.0f), world), ViewProjection);
    output.Color = input.Color;
    output.UV = GetUVPossiblyAnimated(input.UV, DecodeIndexInPoly(input.Effects), DecodeAnimationFrameOffset(input.AnimationFrameOffsetIndexHash));
    output.WorldPosition = mul(float4(input.Position, 1.0f), world).xyz;
    output.Sheen = DecodeSheen(input.Effects);

    float3x3 worldTransform = (float3x3)world;
    TransformObjectTangentBasis(
        input.Normal.xyz,
        input.Tangent.xyz,
        worldTransform,
        output.Normal,
        output.Tangent,
        output.Binormal);
    output.FaceNormal = TransformObjectNormal(input.FaceNormal.xyz, worldTransform);
    
	return output;
}

PixelShaderOutput PS(PixelShaderInput input) : SV_TARGET
{
	if (Animated && Type == 1)
        input.UV = CalculateUVRotate(input.UV, 0);
	
    PixelShaderOutput output;
    
    float4 tex = Texture.Sample(Sampler, input.UV);
    float3 baseColor = tex.xyz * Color.xyz;

    output.Color = float4(baseColor, tex.w * Color.w);

    DoAlphaTest(output.Color);
    
    float4 ORSH = ORSHTexture.Sample(ORSHSampler, input.UV);
    float roughness = ORSH.y;
    float specular = ORSH.z;
	
    float3 emissive = EmissiveTexture.Sample(EmissiveSampler, input.UV).xyz;
	
    float3x3 TBN = float3x3(input.Tangent, input.Binormal, input.Normal);
    float3 normal = UnpackNormalMap(NormalTexture.Sample(NormalTextureSampler, input.UV));
    normal = EnsureNormal(mul(normal, TBN), input.WorldPosition);
    
    output.Color.xyz = CalculateReflections(input.WorldPosition, output.Color.xyz, normal, specular, roughness);
	
    ShaderLight light;
    light.Color = float3(AmbientLight.xyz);
    light.Intensity = 0.3f;
    light.Type = LT_SUN;
    light.Direction = normalize(float3(-1.0f, -0.707f, -0.5f));

    float3 lighting = DoModernDirectionalLight(input.WorldPosition, normal, light);
    lighting += DoModernSpecularSun(input.WorldPosition, normal, light, input.Sheen, specular, roughness);
    lighting += emissive;
    
    output.Color.xyz += lighting * output.Color.a;
    output.Color.xyz = saturate(output.Color.xyz);
    
    output.Emissive = float4(emissive, 1.0f);
	
    return output;
}
